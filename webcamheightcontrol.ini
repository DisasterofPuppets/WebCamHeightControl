/*#######################################################################################
# Disaster of Puppets 2024 -                                                          #
# Instructions and 3D files available:                                                #
# https://github.com/DisasterofPuppets/WebCamHeightControl                            #
#                                                                                     #
# Tutorial and video example http://disasterofpuppets.com                             #
#                                                                                     #
# No warranty provided, because sometimes I don't know what I'm doing.                #
# Test with a dummy servo first or make sure your endstops are functioning.           #
# Have fun :)                                                                         #
#######################################################################################*/

#include <Servo.h>

// Define servo pins
const int servo1Pin = 9; // Connect servo 1 to pin 9
const int servo2Pin = 10; // Connect servo 2 to pin 10
bool TopHit;
bool BottomHit;
bool b1;
bool b2;
bool b3;
bool b4;

// Create servo objects
Servo servo1;
Servo servo2;

// Define button pins
const int button1Pin = 2; // Button 1
const int button2Pin = 3; // Button 2
const int button3Pin = 4; // Button 3
const int button4Pin = 5; // Button 4
const int TopPin = 6; 
const int BottomPin = 7;

void setup() {

  Serial.begin(115200);
  // Attach servos to pins
  servo1.attach(servo1Pin);
  servo2.attach(servo2Pin);


  // Set initial servo positions 
  servo1.write(90); // Center position
  servo2.write(90); // Center position

  // Initialize button pins
  pinMode(button1Pin, INPUT_PULLUP);
  pinMode(button2Pin, INPUT_PULLUP);
  pinMode(button3Pin, INPUT_PULLUP);
  pinMode(button4Pin, INPUT_PULLUP);
  pinMode(TopPin, INPUT_PULLUP);
  pinMode(BottomPin, INPUT_PULLUP);

// Everything is reading as 1 by default / unpressed
}

void loop() {

  TopHit = digitalRead(TopPin);
  BottomHit = digitalRead(BottomPin);
  b1 = digitalRead(button1Pin);
  b2 = digitalRead(button2Pin);
  b3 = digitalRead(button3Pin);
  b4 = digitalRead(button4Pin);

  Serial.print("Top : ");
  Serial.println(TopHit);
  Serial.print("Bottom : ");
  Serial.println(BottomHit);
  Serial.print("1 :");
  Serial.println(b1);
  Serial.print("2 :");
  Serial.println(b2);
  Serial.print("3 :");
  Serial.println(b3);
  Serial.print("4 :");
  Serial.println(b4);
  delay(300);

// Check button states

///////////////////////////UP

  if (b1 == 0 && BottomHit == 1) {
    // Button 1 held: Rotate servo 1 anticlockwise, servo 2 clockwise
    servo1.write(80); // Adjust angle for greater speed 
    servo2.write(100); // Adjust angle for greater speed 
  }
///////////////////////////DOWN

  else if (b2 == 0 && TopHit == 1) {
    // Button 2 held: Rotate servo 1 clockwise, servo 2 anticlockwise
    servo1.write(100); // Adjust angle for greater speed 
    servo2.write(80); // Adjust angle for greater speed 
  }
//////////////////////////AUTO UP
  else if (b3 == 0){
    while (BottomHit == 1) {
        // Button 3 pressed: Rotate servo 1 anticlockwise, servo 2 clockwise automated
        servo1.write(80); // Adjust angle for greater speed // you may need to adjust these or replace with stronger servos
        servo2.write(100); // Adjust angle for greater speed 
        BottomHit=digitalRead(BottomPin);

        // To act as an interrupt
        // Read the other button states
        b1 = digitalRead(button1Pin);
        b2 = digitalRead(button2Pin);
        b4 = digitalRead(button4Pin);
        
        // If any other button is pressed, break out of the while loop
        if (b1 == 0 || b2 == 0 || b4 == 0) {
          break;
        }
        
      }
  }
//////////////////////////AUTO DOWN
  else if (b4 == 0) {
    while (TopHit == 1){
      // Button 5 pressed: Rotate servo 1 clockwise, servo 2 anticlockwise
      servo1.write(97); // Adjust angle for greater speed  // gravity helps here with the values
      servo2.write(83); // Adjust angle for greater speed 
      TopHit=digitalRead(TopPin);

// To act as an interrupt
    // Read the other button states
    b1 = digitalRead(button1Pin);
    b2 = digitalRead(button2Pin);
    b3 = digitalRead(button3Pin);

    // If any other button is pressed, break out of the while loop
      if (b1 == 0 || b2 == 0 || b3 == 0) {
        break;
      }
   }
  }
  else {
    //Stop servos when button 2 is not pressed
    servo1.write(90);
    servo2.write(90);
  }
  
}
